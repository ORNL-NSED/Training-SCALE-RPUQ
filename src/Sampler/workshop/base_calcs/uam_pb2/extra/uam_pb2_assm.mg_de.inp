=t-depl
UAM Phase 1-1 PB2 HFP
v7-56
' Data taken from:
'        Benchmark for Uncertainty Analysis in Modeling (UAM)
'        for Design, Operation and Safety Analyses of LWRs,
'        Nuclear Energy Agency, 2007.
'===============================================================================
read alias
 $fuel  1  2  3  4  5 end
 $gap  11 12 13 14 15 end
 $clad 21 22 23 24 25 end
 $mod  31 32 33 34 35 end
end alias
'===============================================================================
read comp
' 2.93% enriched fuel pin
  uo2   1 den=10.32 1 900 92235 2.93
                          92234 0.0261
                          92236 0.0135
                          92238 97.0304 end
' 1.94% enriched fuel pin
  uo2   2 den=10.32 1 900 92235 1.94
                          92234 0.0173
                          92236 0.0089
                          92238 98.0338 end
' 1.69% enriched fuel pin
  uo2   3 den=10.32 1 900 92235 1.69
                          92234 0.0150
                          92236 0.0078
                          92238 98.2872 end
' 1.33% enriched fuel pin
  uo2   4 den=10.32 1 900 92235 1.33
                          92234 0.0118
                          92236 0.0061
                          92238 98.6521 end
' 3% Gd2O3 by weigh, 2.93% enriched fuel pin
  uo2   5 den=10.19 0.97 900 92235 2.93
                             92234 0.0261
                             92236 0.0135
                             92238 97.0304 end
  gd2o3 5 den=10.19 0.03 900 end
' gap/clad/moderator
  he    $gap  den=4.9559E-4 1 700 end
  zirc2 $clad den=5.678     1 630 end
  h2o   $mod  den=0.4577    1 560 end
' channel
  zirc2 50 den=6.525 1 630 end
' water in bypass
  h2o   51 den=0.738079 1 560 end
end comp
'===============================================================================
read celldata
  latticecell squarepitch hpitch=0.9375  $mod
                           fuelr=0.60579 $fuel
                            gapr=0.62103 $gap
                           cladr=0.71501 $clad end
end celldata
'===============================================================================
read burndata
power=25 burn=30 nlib=1 end
end burndata
'===============================================================================
read depletion
1 2 3 4 5
end depletion
'===============================================================================
read model
NEWT Geometry
'===============================================================================
read parm
 echo=yes drawit=yes
 cmfd=1 inners=2 therm=yes therms=1 xycmfd=4
 epsilon=-3.0e-5
end parm
'===============================================================================
read materials
  mix= 1 pn=1 com='2.93% UO2'         end
  mix=21 pn=1 com='Zirc2 Clad'        end
  mix=51 pn=2 com='moderator'         end
  mix=11 pn=0 com='Helium gap'        end
  mix=31 pn=2 com='coolant'           end
  mix= 2 pn=1 com='1.94% UO2'         end
  mix= 3 pn=1 com='1.69% UO2'         end
  mix= 4 pn=1 com='1.33% UO2'         end
  mix= 5 pn=1 com='2.93% UO2 (3% Gd)' end
  mix=50 pn=1 com='channel'           end
end materials
'===============================================================================
read geom
 unit 1
  cylinder  1 0.60579
  cylinder 11 0.62103
  cylinder 21 0.71501
  cuboid   31 4p0.9375
  media   1 1  1
  media  11 1 11 -1
  media  21 1 21 -11
  media  31 1 31 -21
  boundary 31 2 2
 unit 2
  cylinder  1 0.60579
  cylinder 11 0.62103
  cylinder 21 0.71501
  cuboid   31 4p0.9375
  media   2 1  1
  media  11 1 11 -1
  media  21 1 21 -11
  media  31 1 31 -21
  boundary 31 2 2
 unit 3
  cylinder  1 0.60579
  cylinder 11 0.62103
  cylinder 21 0.71501
  cuboid   31 4p0.9375
  media   3 1  1
  media  11 1 11 -1
  media  21 1 21 -11
  media  31 1 31 -21
  boundary 31 2 2
 unit 4
  cylinder  1 0.60579
  cylinder 11 0.62103
  cylinder 21 0.71501
  cuboid   31 4p0.9375
  media   4 1  1
  media  11 1 11 -1
  media  21 1 21 -11
  media  31 1 31 -21
  boundary 31 2 2
 unit 5
  cylinder  1 0.270917524
  cylinder  2 0.383135237
  cylinder  3 0.469242916
  cylinder  4 0.541835048
  cylinder  5 0.60579
  cylinder 11 0.62103
  cylinder 21 0.71501
  cuboid   31 4p0.9375
  media   5 1 1
  media   5 1 2 -1
  media   5 1 3 -2
  media   5 1 4 -3
  media   5 1 5 -4
  media  11 1 11 -5
  media  21 1 21 -11
  media  31 1 31 -21
  boundary 31 2 2
global unit 100
   cuboid  1 4p6.70306
   cuboid  2 4p6.90626
   cuboid 10 4p7.62
   array 1 1 place 4 4  0.0 0.0
   media 31 1 1
   media 50 1 2 -1
   media 51 1 10 -2
   boundary 10 32 32
end geom
'===============================================================================
read array
 ara=1 nux=7 nuy=7 typ=cuboidal
 fill 3 2 1 1 1 2 2
      2 1 5 1 1 1 2
      2 1 1 1 5 1 1
      2 1 1 1 1 1 1
      3 1 5 1 1 5 1
      3 2 1 1 1 1 2
      4 3 3 2 2 2 3
 end fill
'===============================================================================
end array
'===============================================================================
read collapse
40r1 16r2
end collapse
'================================================================================
read homog
 500 all 1 2 3 4 5 end
end homog
'===============================================================================
end model
'===============================================================================
end
=shell
cp xfile016 "${OUTDIR}/${BASENAME}.x16"
mv _plot000000000000000000 "${OUTDIR}/${BASENAME}.000000000000000000.plt"
end
